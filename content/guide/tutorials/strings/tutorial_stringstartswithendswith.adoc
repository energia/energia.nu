== String StartsWith and EndsWith Functions ==

The String functions link:/reference/en/language/variables/data-types/functions/startswith/[`startsWith()`] and link:/reference/en/language/variables/data-types/functions/endswith[`endsWith()`] allow you to check what character or substring a given String starts or ends with. They're basically special cases of link:/reference/en/language/variables/data-types/functions/substring/[`substring()`].

=== Relevant Groundwork ===

None

=== Hardware Required ===

* MSP-EXP430G2 LaunchPad

=== Circuit ===

Only LaunchPad is required for this example.

image::../img/Blink_bb.png[]

Image developed using http://fritzing.org/home/[Fritzing]. For more circuit examples, see the http://fritzing.org/projects/[Fritzing project page].

=== Schematic ===

image::../img/Blink_schem.png[]

=== Code Explanation ===

`startsWith()` and `endsWith()` can be used to look for a particular message header, or for a single character at the end of a String. They can also be used with an offset to look for a substring starting at a particular position. For example:

----
  stringOne = "HTTP/1.1 200 OK";
  if (stringOne.startsWith("200 OK", 9)) {
    Serial.println("Got an OK from the server"); 
  }
---- 

This is functionally the same as this:

----
  stringOne = "HTTP/1.1 200 OK";
  if (stringOne.substring(9) == "200 OK") {
    Serial.println("Got an OK from the server"); 
  }
----

*Caution:* If you look for a position that's outside the range of the string,you'll get unpredictable results. For example, in the example above `stringOne.startsWith(''200 OK'', 16)` wouldn't check against the String itself, but whatever is in memory just beyond it. For best results, make sure the index values you use for `startsWith` and `endsWith` are between 0 and the String's `length()`.

=== Code ===

----
/*
  String startWith() and endsWith()

 Examples of how to use startsWith() and endsWith() in a String

 created 27 July 2010
 modified 2 Apr 2012
 by Tom Igoe

 This example code is in the public domain.
 */

void setup() {
  // Open serial communications and wait for port to open:
  Serial.begin(9600);

  // send an intro:
  Serial.println("\n\nString startsWith() and endsWith():");
  Serial.println();
}

void loop() {
  // startsWith() checks to see if a String starts with a particular substring:
  String stringOne = "HTTP/1.1 200 OK";
  Serial.println(stringOne);
  if (stringOne.startsWith("HTTP/1.1")) {
    Serial.println("Server's using http version 1.1"); 
  } 

  // you can also look for startsWith() at an offset position in the string:
  stringOne = "HTTP/1.1 200 OK";
  if (stringOne.startsWith("200 OK", 9)) {
    Serial.println("Got an OK from the server"); 
  } 

  // endsWith() checks to see if a String ends with a particular character:
  String sensorReading = "sensor = ";
  sensorReading += analogRead(A0);
  Serial.print (sensorReading);
  if (sensorReading.endsWith(0)) {
    Serial.println(". This reading is divisible by ten"); 
  } 
  else {
    Serial.println(". This reading is not divisible by ten"); 

  }

  // do nothing while true:
  while(true);
}
----

=== Working Video ===

=== Try it out ===

* Blink an LED if a string starts with a vowel.

=== See Also ===

* link:/reference/en/language/structure/sketch/setup/[setup()]
* link:/reference/en/language/structure/sketch/loop/[loop()]
* link:/reference/en/language/functions/communication/serial/serial_println[serial.println()]