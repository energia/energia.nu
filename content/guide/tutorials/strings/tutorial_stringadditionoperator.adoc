== String Addition Operators ==

This example shows how to add strings together with different techniques.

=== Hardware Required ===

* MSP-EXP430G2 LaunchPad

=== Relevant Groundwork ===

You can add Strings together in a variety of ways. This is called concatenation and it results in the original String being longer by the length of the String or character array with which you concatenate it. The `+` operator allows you to combine a String with another String, with a constant character array, an ASCII representation of a constant or variable number, or a constant character.

----
  // adding a constant integer to a string:
  stringThree =  stringOne + 123;

  // adding a constant long interger to a string:
  stringThree = stringOne + 123456789;

  // adding a constant character to a string:
  stringThree =  stringOne + 'A';

  // adding a constant string to a string:
  stringThree =  stringOne +  "abc";

  // adding two Strings together:
  stringThree = stringOne + stringTwo;
---- 

You can also use the `+` operator to add the results of a function to a String, if the function returns one of the allowed data types mentioned above. For example,

----
  stringThree = stringOne + millis();
---- 

This is allowable since the `millis()` function returns a long integer, which can be added to a String. You could also do this:

----
  stringThree = stringOne + analogRead(A0);
---- 

because `analogRead()` returns an integer. String concatenation can be very useful when you need to display a combination of values and the descriptions of those values into one String to display via serial communication, on an LCD display, over an Ethernet connection, or anywhere that Strings are useful. Caution: You should be careful about concatenating multiple variable types on the same line, as you may get unexpected results. For example:

----
  int sensorValue = analogRead(A0); 
  String stringOne = "Sensor value: ";
  String stringThree = stringOne + sensorValue;
  Serial.println(stringThree);
---- 

results in ''Sensor Value: 402'' or whatever the `analogRead()` result is, but

----
  int sensorValue = analogRead(A0); 
  String stringThree = "Sensor value: " + sensorValue;
  Serial.println(stringThree);
---- 

gives unpredictable results because `stringThree` never got an initial value before you started concatenating different data types. Here's another example where improper initialization will cause errors:

----
Serial.println("I want " + analogRead(A0) + " donuts");
----

This won't compile because the compiler doesn't handle the operator precedence correctly. On the other hand, the following will compile, but it won't run as expected:

----
  int sensorValue = analogRead(A0); 
  String stringThree = "I want " + sensorValue;
  Serial.println(stringThree  + " donuts");
---- 

It doesn't run correctly for the same reason as before: `stringThree` never got an initial value before you started concatenating different data types. For best results, initialize your Strings before you concatenate them.

=== Circuit ===

Only LaunchPad is required for this example.

image::../img/Blink_bb.png[]

Image developed using http://fritzing.org/home/[Fritzing]. For more circuit examples, see the http://fritzing.org/projects/[Fritzing project page].

=== Schematic ===

image::../img/Blink_schem.png[]

=== Code Explanation ===

In the program , we first declare three strings. We then demonstrate various concatenation examples.

=== Code ===

----
/*
  Adding Strings together
 Here's a working example of several different concatenation examples
 Examples of how to add strings together
 You can also add several different data types to string, as shown here:

 This example code is in the public domain. 
 */

// declare three strings:
String stringOne, stringTwo, stringThree;

void setup() {
  // initialize serial and wait for port to open:
  Serial.begin(9600);

  stringOne = String("stringThree = ");
  stringTwo = String("this string");
  stringThree = String ();
  // send an intro:
  Serial.println("\n\nAdding strings together (concatenation):");
  Serial.println();
}

void loop() {
  // adding a constant integer to a string:
  stringThree =  stringOne + 123;
  Serial.println(stringThree);    // prints "stringThree = 123"

  // adding a constant long interger to a string:
  stringThree = stringOne + 123456789;
  Serial.println(stringThree);    // prints " You added 123456789"

  // adding a constant character to a string:
  stringThree =  stringOne + 'A';
  Serial.println(stringThree);    // prints "You added A"

  // adding a constant string to a string:
  stringThree =  stringOne +  "abc";
  Serial.println(stringThree);    // prints "You added abc"

  stringThree = stringOne + stringTwo;
  Serial.println(stringThree);    // prints "You added this string"

  // adding a variable integer to a string:
  int sensorValue = analogRead(A0);
  stringOne = "Sensor value: ";
  stringThree = stringOne  + sensorValue;
  Serial.println(stringThree);    // prints "Sensor Value: 401" or whatever value analogRead(A0) has

  // adding a variable long integer to a string:
  long currentTime = millis();
  stringOne="millis() value: ";
  stringThree = stringOne + millis();
  Serial.println(stringThree);    // prints "The millis: 345345" or whatever value currentTime has

  // do nothing while true:
  while(true);
}
----

=== Working Video ===

=== Try it out ===

* Concat multiple strings together and print to serial.

=== See Also ===

* link:/reference/en/language/structure/sketch/setup/[setup()]
* link:/reference/en/language/structure/sketch/loop/[loop()]
* link:/reference/en/language/functions/communication/serial/println/[serial.println()]
 
