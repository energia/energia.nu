== EthernetClient.connected() ==

=== Description ===

Whether or not the client is connected. Note that a client is considered
connected if the connection has been closed but there is still unread
data.

=== Syntax ===

client.connected()

=== Parameters ===

none

=== Returns ===

Returns true if the client is connected, false if not.

=== Example ===

[source,arduino]
----

    #include <Ethernet.h>
    #include <SPI.h>

    byte mac[] = { 0xDE, 0xAD, 0xBE, 0xEF, 0xFE, 0xED };
    byte ip[] = { 10, 0, 0, 177 };
    byte server[] = { 64, 233, 187, 99 }; // Google

    EthernetClient client;

    void setup()
    {
      Ethernet.begin(mac, ip);
      Serial.begin(9600);
      client.connect(server, 80);
      delay(1000);

      Serial.println("connecting...");

      if (client.connected()) {
        Serial.println("connected");
        client.println("GET /search?q=arduino HTTP/1.0");
        client.println();
      } else {
        Serial.println("connection failed");
      }
    }

    void loop()
    {
      if (client.available()) {
        char c = client.read();
        Serial.print(c);
      }

      if (!client.connected()) {
        Serial.println();
        Serial.println("disconnecting.");
        client.stop();
        for(;;)
          ;
      }
    }
----